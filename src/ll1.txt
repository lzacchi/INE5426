PROGRAM ::= STATEMENT
PROGRAM ::= FUNCLIST
PROGRAM ::= empty

FUNCLIST ::= FUNCDEF FUNCLISTTMP

FUNCLISTTMP ::= FUNCLIST
FUNCLISTTMP ::= empty

FUNCDEF ::= FUNCTION_DECLARATION LABEL LEFT_PARENTHESIS PARAMLIST RIGHT_PARENTHESIS LEFT_BRACKET STATELIST RIGHT_BRACKET

PARAMLIST ::= DATATYPE LABEL PARAMLISTTMP
PARAMLIST ::= empty

PARAMLISTTMP ::= COMMA PARAMLIST
PARAMLISTTMP ::= empty

DATATYPE ::= INTEGER
DATATYPE ::= FLOATING_POINT
DATATYPE ::= STRING

STATEMENT ::= VARDECL SEMICOLON
STATEMENT ::= ATRIBSTAT SEMICOLON
STATEMENT ::= PRINTSTAT SEMICOLON
STATEMENT ::= READSTAT SEMICOLON
STATEMENT ::= RETURNSTAT SEMICOLON
STATEMENT ::= IFSTAT
STATEMENT ::= FORSTAT
STATEMENT ::= LEFT_BRACKET STATELIST RIGHT_BRACKET
STATEMENT ::= BREAK SEMICOLON
STATEMENT ::= SEMICOLON

VARDECL ::= DATATYPE LABEL OPTIONAL_VECTOR

OPTIONAL_VECTOR ::= LEFT_SQUARE_BRACKET INTEGER_CONSTANT RIGHT_SQUARE_BRACKET OPTIONAL_VECTOR
OPTIONAL_VECTOR ::= empty

ATRIBSTAT ::= LVALUE ATTRIBUTION ATRIB_RIGHT

ATRIB_RIGHT ::= EXPRESSION_OR_FUNCCALL
ATRIB_RIGHT ::= ALLOCEXPRESSION
EXPRESSION_OR_FUNCCALL ::= PLUS FACTOR RECURSIVE_UNARYEXPR RECURSIVE_MINUS_OR_PLUS OPTIONAL_REL_OP_NUMEXPRESSION
EXPRESSION_OR_FUNCCALL ::= MINUS FACTOR RECURSIVE_UNARYEXPR RECURSIVE_MINUS_OR_PLUS OPTIONAL_REL_OP_NUMEXPRESSION
EXPRESSION_OR_FUNCCALL ::= INTEGER_CONSTANT RECURSIVE_UNARYEXPR RECURSIVE_MINUS_OR_PLUS OPTIONAL_REL_OP_NUMEXPRESSION
EXPRESSION_OR_FUNCCALL ::= FLOATING_POINT_CONSTANT RECURSIVE_UNARYEXPR RECURSIVE_MINUS_OR_PLUS OPTIONAL_REL_OP_NUMEXPRESSION
EXPRESSION_OR_FUNCCALL ::= STRING_CONSTANT RECURSIVE_UNARYEXPR RECURSIVE_MINUS_OR_PLUS OPTIONAL_REL_OP_NUMEXPRESSION
EXPRESSION_OR_FUNCCALL ::= NULL RECURSIVE_UNARYEXPR RECURSIVE_MINUS_OR_PLUS OPTIONAL_REL_OP_NUMEXPRESSION
EXPRESSION_OR_FUNCCALL ::= LEFT_PARENTHESIS NUMEXPRESSION RIGHT_PARENTHESIS RECURSIVE_UNARYEXPR RECURSIVE_MINUS_OR_PLUS OPTIONAL_REL_OP_NUMEXPRESSION
EXPRESSION_OR_FUNCCALL ::= LABEL FOLLOW_LABEL

FOLLOW_LABEL::= OPTIONAL_ALLOC_NUMEXPRESSION RECURSIVE_UNARYEXPR RECURSIVE_MINUS_OR_PLUS OPTIONAL_REL_OP_NUMEXPRESSION
FOLLOW_LABEL::= LEFT_PARENTHESIS PARAMLISTCALL RIGHT_PARENTHESIS

PARAMLISTCALL ::= LABEL PARAMLISTCALLTMP
PARAMLISTCALL ::= empty

PARAMLISTCALLTMP ::= COMMA PARAMLISTCALL
PARAMLISTCALLTMP ::= empty

PRINTSTAT ::= PRINT EXPRESSION

READSTAT ::= READ LVALUE

RETURNSTAT ::= RETURN

IFSTAT ::= IF LEFT_PARENTHESIS EXPRESSION RIGHT_PARENTHESIS LEFT_BRACKET STATELIST RIGHT_BRACKET OPTIONAL_ELSE

OPTIONAL_ELSE ::= ELSE LEFT_BRACKET STATELIST RIGHT_BRACKET
OPTIONAL_ELSE ::= empty

FORSTAT ::= FOR LEFT_PARENTHESIS ATRIBSTAT SEMICOLON EXPRESSION SEMICOLON ATRIBSTAT RIGHT_PARENTHESIS LEFT_BRACKET STATELIST RIGHT_BRACKET

STATELIST ::= STATEMENT OPTIONAL_STATELIST

OPTIONAL_STATELIST ::= STATELIST
OPTIONAL_STATELIST ::= empty

ALLOCEXPRESSION ::= NEW DATATYPE LEFT_SQUARE_BRACKET NUMEXPRESSION RIGHT_SQUARE_BRACKET OPTIONAL_ALLOC_NUMEXPRESSION

OPTIONAL_ALLOC_NUMEXPRESSION ::= LEFT_SQUARE_BRACKET NUMEXPRESSION RIGHT_SQUARE_BRACKET OPTIONAL_ALLOC_NUMEXPRESSION
OPTIONAL_ALLOC_NUMEXPRESSION ::= empty

EXPRESSION ::= NUMEXPRESSION OPTIONAL_REL_OP_NUMEXPRESSION
OPTIONAL_REL_OP_NUMEXPRESSION ::= REL_OP NUMEXPRESSION
OPTIONAL_REL_OP_NUMEXPRESSION ::= empty

REL_OP ::= LESSER_THAN
REL_OP ::= GREATER_THAN
REL_OP ::= LESS_OR_EQUAL_THAN
REL_OP ::= GREATER_OR_EQUAL_THAN
REL_OP ::= EQUAL
REL_OP ::= NOT_EQUAL

NUMEXPRESSION ::= TERM RECURSIVE_MINUS_OR_PLUS
RECURSIVE_MINUS_OR_PLUS ::= MINUS_OR_PLUS TERM RECURSIVE_MINUS_OR_PLUS
RECURSIVE_MINUS_OR_PLUS ::= empty

MINUS_OR_PLUS ::= PLUS
MINUS_OR_PLUS ::= MINUS

TERM ::= UNARYEXPR RECURSIVE_UNARYEXPR

RECURSIVE_UNARYEXPR ::= UNARYEXPR_OPERATOR TERM
RECURSIVE_UNARYEXPR ::= empty

UNARYEXPR_OPERATOR ::= TIMES
UNARYEXPR_OPERATOR ::= DIVISION
UNARYEXPR_OPERATOR ::= MODULO

UNARYEXPR ::= MINUS_OR_PLUS FACTOR
UNARYEXPR ::= FACTOR

FACTOR ::= INTEGER_CONSTANT
FACTOR ::= FLOATING_POINT_CONSTANT
FACTOR ::= STRING_CONSTANT
FACTOR ::= NULL
FACTOR ::= LVALUE
FACTOR ::= LEFT_PARENTHESIS NUMEXPRESSION RIGHT_PARENTHESIS

LVALUE ::= LABEL OPTIONAL_ALLOC_NUMEXPRESSION

empty ::= ''
